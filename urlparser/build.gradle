plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.72'
    id 'org.jetbrains.dokka' version '0.10.1'
}
repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'


//ext.docsDir = new File(project.rootDir, "documentation")
ext.docsDir = file("$project.rootDir/documentation")
println(docsDir)
println()

kotlin {
    jvm()
    ios()

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
    }
}

dokka {
    outputFormat = "javadoc"
    outputDirectory = file("$docsDir/javadoc")
}
task dokkaMD(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "markdown"
    outputDirectory = file("$docsDir/markdown")
}
configure(dokkaMD) {
    group = 'documentation'
    description = 'Generate Markdown Docs'
}
build.finalizedBy(dokka, dokkaMD)
clean.doFirst { delete docsDir }

task('bumpBuildVersion') {
    doLast {
        def propsFile = new File(project.rootDir, "gradle.properties")
        if (!propsFile.exists())
            throw new GradleException("gradle.properties not found at $propsFile")

        def props = new Properties()
        propsFile.withInputStream { stream -> props.load(stream) }

        if (!props.containsKey("version"))
            throw new GradleException("no version defined in gradle.properties at $propsFile")

        def split = props.getProperty('version').split("\\.")
        if (split.size() != 3)
            throw new GradleException("expected 3 part semantic version in gradle.properties at $propsFile")

        split[2] = (Integer.parseInt(split[2]) + 1).toString()
        props.setProperty('version', split.join('.'))
        propsFile.withOutputStream { stream -> props.store(stream, "") }
    }
}
configure(bumpBuildVersion) {
    group = 'build'
    description = 'Bump the semantic build version by 1'
}